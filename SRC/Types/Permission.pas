unit Permission;

interface

uses
  Classes, Db, DbClient, DbTables, Item, NamedItem;

type
  TPermScrollEvent = procedure of object;

  TPerm = class(TNamedItem)
  private
    QryPerms, QryAffApps, QryGrantUsrs: TQuery;
    CdsPermBuff: TClientDataSet;

    FilterAppId: Integer;
    App, User, PermApps, UserApps: TItem;

    procedure PrepDataSets;
    procedure FreeDataSets;

    procedure InitDataObjs;
    procedure FreeDataObjs;

    procedure PrepPermsQry;
    procedure PrepAffAppsQry;
    procedure PrepGrantUsrsQry;

    procedure OpenPerms;
    procedure OpenAffApps(PermId: Integer);
    procedure OpenGrantUsrs(AppId: Integer);

    function GetPermBuff: TDataSet;

    procedure PermAfterScroll(DataSet: TDataSet);
    procedure ProcessPermissionChange;
  public
    OnPermAdd: TItemAddDelEvent;
    OnPermUpd: TItemAddDelEvent;
    OnPermDel: TItemAddDelEvent;
    OnPermScroll: TPermScrollEvent;

    OnAffAppAdd: TItemAddDelEvent;
    OnAffAppRevoke: TItemAddDelEvent;

    // М№­ЯРвтьУт ыЫ­зЖЫз­Ьж рЬуч СЯььчш
    procedure CreateTable; override;
    procedure DropTable; override;
    function DatasetName: ShortString; override;
    function FieldDefs: String; override;

    // ЛЬуСЯьУт/зСЯвтьУт ­Яу­тЭтьУ 
    class function Open(FilterAppId: Integer = 0): TPerm;
    class procedure Release(Perm: TPerm);

    // ─ЬыЫз№ Ж СЯььчВ
    function GetPerms: TDataSet;
    function GetAffApps: TDataSet;
    function GetGrantUsrs(AppId: Integer): TDataSet;

    // ╩ЬВЯьСч з№­ЯРвтьУ  ЬрЩтЖЫЬВ
    procedure SetFilter(AppId: Integer);

    // ╩ЬВЯьСч ВЯьУ№звУ­ЬРЯьУ  СЯььчВУ
    function AddPerm: TDataSet;
    function UpdatePerm: TDataSet;
    procedure DeletePerm(PermId: Integer);
    procedure ApplyUpdate;
    procedure CancelUpdate;

    procedure AddAffApp(AppId: Integer);
    procedure RevokeApp(AppId: Integer);

    procedure GrantPerm(UserId, AppId: Integer);
    procedure RevokePerm(UserId, AppId: Integer);
  end;

implementation

uses
  App, CdsLib, DsLib, List, QryLib, TextLib, User, Variants;

//------------------------------------------------------------------------------
// ЛЬуСЯьУт ЫЯрвУШ №­У №т­РЬВ уЯ№зыЖт №­УвЬТтьУ  УвУ №­У №­УьзСУЫтвЧьЬж
// УьУШУЯвУуЯШУУ рЯуч СЯььчш РЬ Р­тВ  ЫтыЫУ­ЬРЯьУ .
//------------------------------------------------------------------------------
procedure TPerm.CreateTable;
var
  Entity: TItem;
begin
  inherited CreateTable;

  // Л№УыЬЖ №­УвЬТтьУж, Ж ЖЬЫЬ­чВ №­УВтьУВЬ ­Яу­тЭтьУт
  Entity := TApp.Create;
  with TItemList.Create(Self, Entity) do begin
    CreateTable;
    Free;
  end;
  Entity.Free;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// МСЯвтьУт ЫЯрвУШ №­У №­УьзСУЫтвЧьЬж УьУШУЯвУуЯШУУ рЯуч СЯььчш РЬ Р­тВ 
// ЫтыЫУ­ЬРЯьУ .
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.DropTable;
var
  Entity: TItem;
begin
  Entity := TApp.Create;
  with TItemList.Create(Self, Entity) do begin
    DropTable;
    Free;
  end;;
  Entity.Free;

  inherited DropTable;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// ╚В  ЫЯрвУШч рЯуч СЯььчш, ыР уЯььЬж ы §ЫЬж ызщьЬыЫЧ■.
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.DatasetName: ShortString;
begin
  Result := 'Perm';
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// ¤Ьв  ЫЯрвУШч рЯуч СЯььчш.
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.FieldDefs: String;
begin
  Result := inherited FieldDefs;
  // ¤­УуьЯЖ №­УВтьУВЬыЫУ ­Яу­тЭтьУ  ЖЬ РытВ №­УвЬТтьУ В
  Result := TextUtils.ConcatStr(Result, 'AllApps Char(1)', ',');
end;

//------------------------------------------------------------------------------
// ЛЬуСЯьУт ЬрЩтЖЫЯ №­У ­ЯрЬЫт №­УвЬТтьУ .
//------------------------------------------------------------------------------
class function TPerm.Open(FilterAppId: Integer = 0): TPerm;
begin
  Result := TPerm.Create;

  with Result do begin
    InitDataObjs;
    PrepDataSets;

    OpenAffApps(0);
    SetFilter(FilterAppId);
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
class procedure TPerm.Release(Perm: TPerm);
begin
  with Perm do begin
    FreeDataSets;
    FreeDataObjs;
    Free;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.InitDataObjs;
begin
  App := TApp.Create;
  User := TUser.Create;
  PermApps := TItemList.Create(Self, App);
  UserApps := TItemList.Create(User, PermApps);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.FreeDataObjs;
begin
  App.Free;
  User.Free;
  PermApps.Free;
  UserApps.Free;
end;

//------------------------------------------------------------------------------
procedure TPerm.PrepDataSets;
begin
  PrepPermsQry;
  with QryPerms do begin
    CachedUpdates := true;
    UpdateObject := TUpdateSQL.Create(nil);
    AfterScroll := PermAfterScroll;
  end;

  PrepAffAppsQry;
  with QryAffApps do begin
    CachedUpdates := true;
    UpdateObject := TUpdateSQL.Create(nil);
  end;

  PrepGrantUsrsQry;
  with QryGrantUsrs do begin
    CachedUpdates := true;
    UpdateObject := TUpdateSQL.Create(nil);
  end;

  PopulateUpdSQL(Self, QryPerms.UpdateObject as TUpdateSQL);
  PopulateUpdSQL(PermApps, QryAffApps.UpdateObject as TUpdateSQL);
  PopulateUpdSQL(UserApps, QryGrantUsrs.UpdateObject as TUpdateSQL);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// КЯ№­Ьы ьЯ ­Яу­тЭтьУ  ы зэИЫЬВ ЗУвЧЫ­Я №­УвЬТтьУж.
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.PrepPermsQry;
begin
  QryPerms := TQuery.Create(nil);
  QryUtils.InitQuery(QryPerms);

  with QryPerms do begin
    SQL.Text :=
      'select distinct p.* ' +
      'from ' + DecorDsName + ' p ' +
        'left join ' + PermApps.DecorDsName + ' pa ' +
          'on pa.' + RefFldName + ' = p.Id ' +
      'where pa.' + App.RefFldName + ' = :FilterAppId ' +
        'or :FilterAppId = 0';
    Prepare;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// КЯ№­Ьы Св  №­УвЬТтьУж, Ж ЖЬЫЬ­чВ ВЬТьЬ №­УВтьУЫЧ ЫтЖзщтт Р QryPerm
// ­Яу­тЭтьУт.
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.PrepAffAppsQry;
begin
  QryAffApps := TQuery.Create(nil);
  QryUtils.InitQuery(QryAffApps);

  with QryAffApps do begin
    SQL.Text :=
      'select * ' +
      'from ' + App.DecorDsName + ' a ' +
        'join ' + PermApps.DecorDsName + ' pa ' +
          'on pa.' + App.RefFldName + ' = a.' + App.FieldName('Id') + ' ' +
      'where pa.' + RefFldName + ' = :PermId';
    Prepare;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// КЯ№­Ьы Св  №ЬвЧуЬРЯЫтвтж, ЖЬЫЬ­чВ №­тСЬыЫЯРвтьЬ ЫтЖзщтт Р QryPerm ­Яу­тЭтьУт,
// ы зэИЫЬВ ЗУвЧЫ­Я №­УвЬТтьУж.
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.PrepGrantUsrsQry;
begin
  QryGrantUsrs := TQuery.Create(nil);
  QryUtils.InitQuery(QryGrantUsrs);

  with QryGrantUsrs do begin
    SQL.Text :=
      'select * ' +
      'from ' + User.DecorDsName + ' u ' +
        'join ' + UserApps.DecorDsName + ' upa ' +
          'on upa.' + User.RefFldName + ' = u.' + User.FieldName('Id') + ' ' +
        'join ' + PermApps.DecorDsName + ' pa ' +
          'on pa.Id = upa.' + PermApps.RefFldName + ' ' +
        'join ' + App.DecorDsName + ' a ' +
          'on a.' + App.FieldName('Id') + ' = pa.' + App.RefFldName + ' ' +
      'where pa.' + RefFldName + ' = :PermId ' +
        'and pa.' + App.RefFldName + ' = :AppId';
    Prepare;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
//
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.OpenPerms;
begin
  with QryPerms do begin
    DisableControls;
    Close;

    ParamByName('FilterAppId').AsInteger := FilterAppId;
    Open;

    EnableControls;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.OpenAffApps(PermId: Integer);
begin
  with QryAffApps do begin
    DisableControls;
    Close;

    ParamByName('PermId').AsInteger := PermId;
    Open;

    EnableControls;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.OpenGrantUsrs(AppId: Integer);
begin
  with QryGrantUsrs do begin
    DisableControls;
    Close;

    ParamByName('PermId').AsInteger := QryPerms.FieldByName('Id').AsInteger;
    ParamByName('AppId').AsInteger := AppId;
    Open;

    EnableControls;
  end;
end;

//------------------------------------------------------------------------------
// ─ЬыЫз№ Ж СЯььчВ
//------------------------------------------------------------------------------
function TPerm.GetPerms: TDataSet;
begin
  Result := QryPerms;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.GetAffApps: TDataSet;
begin
  Result := QryAffApps;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.GetGrantUsrs(AppId: Integer): TDataSet;
begin
  OpenGrantUsrs(AppId);

  Result := QryGrantUsrs;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.GetPermBuff: TDataSet;
begin
  if CdsPermBuff = nil then
    CdsPermBuff := CdsUtils.CreateCds(QryPerms);

  Result := CdsPermBuff;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
// ¤Ь УуВтьтьУУ №ЬуУШУУ Р лЯу­тЭтьУ ш, №т­туЯс­зТЯтВ №­УвЬТтьУ 
//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.PermAfterScroll(DataSet: TDataSet);
begin
  ProcessPermissionChange;
end;

//------------------------------------------------------------------------------
procedure TPerm.ProcessPermissionChange;
begin
  OpenAffApps(QryPerms.FieldByName('Id').AsInteger);

  if Assigned(OnPermScroll) then
    OnPermScroll;
end;

//------------------------------------------------------------------------------
// ╩ЬВЯьСч з№­ЯРвтьУ  ЬрЩтЖЫЬВ
//------------------------------------------------------------------------------
procedure TPerm.SetFilter(AppId: Integer);
begin
  FilterAppId := AppId;
  OpenPerms;
  ProcessPermissionChange;
end;

//------------------------------------------------------------------------------
// ╩ЬВЯьСч ВЯьУ№звУ­ЬРЯьУ  СЯььчВУ
//------------------------------------------------------------------------------
function TPerm.AddPerm: TDataSet;
begin
  Result := GetPermBuff;
  Result.Append;
  Result.FieldByName('AllApps').AsString := ' ';
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
function TPerm.UpdatePerm: TDataSet;
begin
  Result := GetPermBuff;
  Result.Edit;
  DsUtils.CopyRecord(QryPerms, Result);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.ApplyUpdate;
var
  NewRec: Boolean;
begin
  with GetPermBuff do begin
    Post;

    NewRec := FieldByName('Id').AsInteger = 0;

    with QryPerms do begin
      if NewRec then
        Append
      else
        Edit;

      DsUtils.CopyRecord(CdsPermBuff, QryPerms);

      if NewRec then
        FieldByName('Id').AsInteger := QryUtils.GenerateId(DecorDsName);

      Post;

      Database.ApplyUpdates([QryPerms]);
      CommitUpdates;

      if NewRec then begin
        if Assigned(OnPermAdd) then
          OnPermAdd(FieldByName('Id').AsInteger);
      end else
        if Assigned(OnPermUpd) then
          OnPermUpd(FieldByName('Id').AsInteger);
    end;

    Delete;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.CancelUpdate;
begin
  with GetPermBuff do begin
    Cancel;

    if RecordCount > 0 then
      Delete;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.DeletePerm(PermId: Integer);
begin
  QryPerms.Delete;

  if Assigned(OnPermDel) then
    OnPermDel(PermId);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.AddAffApp(AppId: Integer);
begin
  with QryAffApps do begin
    Append;
    FieldByName('Id').AsInteger := QryUtils.GenerateId(DecorDsName);
    FieldByName(RefFldName).AsInteger := QryPerms.FieldByName('Id').AsInteger;
    FieldByName(App.RefFldName).AsInteger := AppId;
    Post;

    Database.ApplyUpdates([QryAffApps]);
    CommitUpdates;
  end;

  if Assigned(OnAffAppAdd) then
    OnAffAppAdd(AppId);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.RevokeApp(AppId: Integer);
begin
  with QryAffApps do begin
    Locate(
      RefFldName + ';' + App.RefFldName,
      VarArrayOf([QryPerms.FieldByName('Id').AsInteger, AppId]), []
    );
    Delete;

    Database.ApplyUpdates([QryAffApps]);
    CommitUpdates;
  end;

  if Assigned(OnAffAppRevoke) then
    OnAffAppRevoke(AppId);
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.GrantPerm(UserId, AppId: Integer);
var
  PermAppId: Integer;
begin
  // ═ЯшЬСУВ ¤­УвЬТтьУт ЫтЖзщтсЬ лЯу­тЭтьУ 
  with QryAffApps do begin
    Locate(RefFldName + ';' + App.RefFldName,
      VarArrayOf([QryPerms.FieldByName('Id').AsInteger, AppId]), []);

    PermAppId := FieldByName('Id').AsInteger;
  end;

  with QryGrantUsrs do begin
    Append;
    FieldByName('Id').AsInteger := QryUtils.GenerateId(DecorDsName);
    FieldByName(User.RefFldName).AsInteger := UserId;
    FieldByName(PermApps.RefFldName).AsInteger := PermAppId;
    Post;

    Database.ApplyUpdates([QryGrantUsrs]);
    CommitUpdates;
  end;
end;

//ииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииииии
procedure TPerm.RevokePerm(UserId, AppId: Integer);
begin
end;

//------------------------------------------------------------------------------
procedure TPerm.FreeDataSets;
begin
  QryPerms.UpdateObject.Free;
  QryAffApps.UpdateObject.Free;
  QryGrantUsrs.UpdateObject.Free;

  QryPerms.Free;
  QryAffApps.Free;
  QryGrantUsrs.Free;

  if CdsPermBuff <> nil then
    CdsPermBuff.Free;
end;

end.
